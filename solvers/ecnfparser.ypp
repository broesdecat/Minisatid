%code requires{
#include <iostream>
#include <stdio.h>
#include <cstring>
#include <vector>
#include <list>
#include <map>

#include <tr1/memory>
using namespace std::tr1;

#include "solvers/ExternalInterface.hpp"

}

%code provides{

extern ECNF_mode modes;

//extern int ecnflex(void);
//extern char * ecnftext;
extern int yylex(void);
extern char * yytext;

bool mod;
shared_ptr<PropositionalSolver>		solver;
shared_ptr<ModalSolver>	modsolver;

vector<Weight> weights;
vector<Atom> nb, rigidatoms;
vector<Literal> lits;
bool disj;
int setid, modid;
bool unsatfound = false;
bool parseError = false;

bool solverdecided = false, negatemod = false;
int cnfcurrentmodid = 0;
Atom cnfcurrenthead(-1);
int atoms, clauses;
vector<int> modalops;
vector<Literal> heads;

/**
 * Initializes the solvers to add the datastructures
 */
void yyinit(){
	unsatfound = false;
}

/**
 * Removes the solvers, releasing their locks and empties the temporary datastructures
 */
void yydestroy(){
	solver.reset();
	modsolver.reset();
	weights.clear();
	lits.clear();
}

//Auxiliary variables, used while parsing.
int lineNo = 1;
int charPos = 1;

// If an unforeseen parse error occurs, it calls this function (e.g. with msg="syntax error")
void yyerror(const char* msg) {
	if(unsatfound){
		cerr << msg;
	}else{
		cerr << "Parse error: ";
		cerr << "Line " << lineNo << ", column " << charPos << ": "; 
		cerr << msg;
		if (strlen(/*ecnftext*/yytext)){
			cerr << " on \"" << /*ecnftext*/yytext << "\"";		
		}
		cerr << endl;
		parseError = true;
	}	
	throw idpexception();
}

AggrType getAggrType(int type){
	return static_cast<AggrType>(type);
}


/**
 * CR-CheckResult: checks the result of passing some data to the solvers.
 * The result is false if unsat was already detected.
 */
void CR(bool result){
	if(!result){
		unsatfound = true;
		yyerror("Unsat was found during parsing.\n");
	}		
}

/**
 * Reads the new literal and adds it to the current list of literals
 */
inline void addLit(int nb){
	lits.push_back(nb);
}

void initSolver(){
	solver = shared_ptr<PropositionalSolver>(new PropositionalSolver(modes));
	mod = false;
	solverdecided = true;
}

void initModSolver(){
	modsolver = shared_ptr<ModalSolver>(new ModalSolver(modes));
	mod = true;
	solverdecided = true;
}

shared_ptr<SolverInterface> getData(){
	if(solver.get()!=NULL){
		return solver;
	}else{
		return modsolver;
	}
}

}

//for a reentrant parser, you need to use %pureparser

%union {
	int integer;
    bool boolean;
    vector<int>* vint;
    MINISAT::EqType rel;
}

%token EQ DISJUNCTION CONJUNCTION
%token SUBSETMINDEFN MNMZDEFN SUMMINDEFN
%token SETDEFN WSETDEFN  
%token <integer> ZERO NUMBER AGGDEFN
%token <boolean> SEMDEFN SIGNDEFN QUANTe
%token CNF ECNF DEFPRESENT AGGPRESENT MNMZPRESENT MODPRESENT  MODDEFN
%token INTVAR SUMTRT SUMTRI SUMTIRT SUMTIRI COUNTTRI COUNTTRT DISTINCT REL DELIM

%type <integer> number
%type <vint> intlist

%start init

%%

init	:	CNF	number number { atoms = $2; clauses = $3; } ctheory
		|	ECNF header {initSolver();} theory
		|	ECNF header MODPRESENT {initModSolver();} mtheory
		;

header	: 	/*empty*/
		| 	header DEFPRESENT	{ modes.def = true;}
		| 	header AGGPRESENT	{ modes.aggr = true; }
		| 	header MNMZPRESENT	{ modes.mnmz = true; }
		;
		
ctheory	: 	cmhier {
				if(!solverdecided){
					initSolver();
				}else{
					if(negatemod){
						reportf("The quantifiers require to negate the full theory, this is "
								"not handled at the moment.\n");
						throw idpexception();
					}
					for(vector<int>::size_type i = 0; i<modalops.size(); i++){
						lits.push_back(heads[i]);
						CR(modsolver->addClause(modalops[i], lits)); lits.clear();	
					}
				}
			} cclauses
		;

cmhier	:	/* empty */
		|	QUANTe { 
				bool writeexists;
				if($1 && negatemod){
					negatemod = false;
					writeexists = false;
				}else if($1 && !negatemod){
					negatemod = false;
					writeexists = true;
				}else if(!$1 && negatemod){
					negatemod = true;
					writeexists = true;
				}else{
					negatemod = true;
					writeexists = false;
				}
				if(!solverdecided){
					initModSolver();
					cnfcurrenthead = atoms;
				}else{
					modalops.push_back(cnfcurrentmodid);
					if(writeexists){
						heads.push_back(Literal(cnfcurrenthead, false));
					}else{
						heads.push_back(Literal(cnfcurrenthead, true));
					}
					cnfcurrentmodid++;
					CR(modsolver->addChild(cnfcurrentmodid-1, cnfcurrentmodid, Literal(cnfcurrenthead)));
					modsolver->addAtoms(cnfcurrentmodid, rigidatoms);
					if(rigidatoms.size()!=0){
						throw idpexception();
					}
					cnfcurrenthead = Atom(cnfcurrenthead.getValue());
				}
			}
			rigidvarbody ZERO cmhier
		;

//FIXME: dit blijkt nodig om de performantie omhoog te krikken, eens onderzoeken waarom
cclauses:	/* empty */
		|	cclauses cclause
		;
			
cclause: body ZERO	{
				if(mod){
					CR(modsolver->addClause(cnfcurrentmodid, lits)); lits.clear();
				}else{
					CR(solver->addClause(lits)); lits.clear();
				}
			}
		;


theory	:	/* empty */
		|	theory clause
		|	theory rule
		| 	theory agg
		|	theory set
		|	theory wset
		|	theory mnmz
		|	theory subsetmnmz
		|	theory summnmz
		|	theory intvar
		|	theory cpconstraint
		;
		
mtheory	:	mhier mrest
		;

mhier	:	/* empty */
		|	mhier matomset
		|	mhier modhier
		;
		
mrest	: 	/* empty */
		|	mrest mclause
		|	mrest mrule
		| 	mrest magg
		|	mrest mset
		|	mrest mwset
		;
			
mnmz	:	MNMZDEFN body ZERO				{ CR(solver->addMinimize(lits, false)); lits.clear();}
subsetmnmz: SUBSETMINDEFN body ZERO 		{ CR(solver->addMinimize(lits, true)); lits.clear();}
summnmz :	SUMMINDEFN NUMBER NUMBER ZERO	{ CR(solver->addSumMinimize(Atom($2), $3));}

body	:  /* empty */
		|  body NUMBER { addLit($2); }
		;
		
varbody	:  /* empty */
		|  varbody NUMBER	
			{ 
				if($2<0){yyerror("Rigid atoms cannot have a sign.\n");}
				nb.push_back(Atom($2)); 
			}
		;
		
rigidvarbody	
		:  /* empty */
		|  rigidvarbody NUMBER	
			{ 
				if($2<0){yyerror("Rigid atoms cannot have a sign.\n");}
				rigidatoms.push_back(Atom($2)); 
			}
		;		
         
wbody	:	/* empty */
		|	wbody NUMBER EQ ZERO	{ addLit($2); weights.push_back(Weight(0)); }
		|	wbody NUMBER EQ NUMBER	{ addLit($2); weights.push_back(Weight($4)); }
		;
       
		
//NONMODAL VERSION
		
clause	:  body ZERO	{ CR(solver->addClause(lits)); lits.clear(); }
		;
rule	:	SEMDEFN NUMBER                  
						{ 	if ($2 < 0) yyerror("Encountered a rule with negative literal as head.\n");
							addLit($2);
							disj = $1;
						}
			body ZERO  	{ CR(solver->addRule(!disj, lits)); lits.clear(); }
		;
agg		:	AGGDEFN  SEMDEFN SIGNDEFN NUMBER NUMBER NUMBER ZERO	{ CR(solver->addAggrExpr(Literal($4), $5, Weight($6), $3, getAggrType($1), $2)); }
		;
agg		:	AGGDEFN  SEMDEFN SIGNDEFN NUMBER NUMBER ZERO ZERO	{ CR(solver->addAggrExpr(Literal($4), $5, Weight(0), $3, getAggrType($1), $2)); }
		;

set		:	SETDEFN NUMBER { setid = $2;	}
			body ZERO	{ 
							for(vector<Literal>::size_type i=0; i<lits.size(); i++){
								weights.push_back(1);
							}
							CR(solver->addSet(setid, lits, weights));
							lits.clear(); weights.clear();
						}
		;

wset	:	WSETDEFN NUMBER { setid = $2;	}
			wbody ZERO	{ 
							CR(solver->addSet(setid, lits, weights));
							lits.clear(); weights.clear();
						}
		;

intvar	:	INTVAR number number number ZERO { solver->addIntVar($2, $3, $4); }

termlist : /* empty */ //TODO cannot be empty!
		|  termlist NUMBER
			{ 
				if($2<1){yyerror("Term identifers cannot be < 1.\n");}
				nb.push_back($2); 
			}
		;
		
intlist :  /* empty */
		|  intlist NUMBER
			{ 
				nb.push_back($2); 
			}
		;

cpconstraint	:	/*SUMTRT NUMBER termlist REL NUMBER ZERO {solver->addSum($2, $3, $4, $5);}
				|	SUMTRI NUMBER termlist REL number ZERO {solver->addSumVar($2, $3, $4, $5);}
				|	SUMTIRT NUMBER termlist DELIM intlist REL NUMBER ZERO {solver->addSum($2, $3, $5, $6, $7);}
				|	SUMTIRI NUMBER termlist DELIM intlist REL number ZERO {solver->addSumVar($2, $3, $5, $6, $7);}
//				|	COUNTTRI termlist REL number ZERO
				|	COUNTTRT termlist DELIM number REL NUMBER ZERO { solver->addCount($2, $4, $5, $6); }
//				| 	DISTINCT termlist REL ZERO { solver->addDistinct($2 $3); }*/
				;

//MODAL PART: USES INDEXES+1 AS MODAL IDs IN THE THEORY
	//FIXME: probleem gevonden: de HEAD kan niet correct aan de juiste worden toegevoegd, 
	//want de parent is nog niet gekend! => ALLE MODS MOETEN EERST IN DE THEORIE!!!
	//FIXME 2: je kan nu vars enzo toevoegen terwijl de kinderen van een mod op nog niet gekend
	//zijn, wat dus onvolledige kennis geeft!
modhier :	MODDEFN	NUMBER 	NUMBER 	NUMBER ZERO 
				{ CR(modsolver->addChild($2-1, $3-1, Literal($4))); }
		;
matomset:	QUANTe NUMBER 
				{ 
					if(!$1){
						reportf("Forall quantifiers are not part of the chosen input format.\n");
						throw idpexception();
					}
					modid = $2-1; 
				}
			varbody ZERO { modsolver->addAtoms(modid, nb); nb.clear(); }
		;
mclause	:	NUMBER {modid = $1-1;} 
			body ZERO { CR(modsolver->addClause(modid, lits)); lits.clear(); }
		;
mrule	:	SEMDEFN NUMBER {disj = $1; modid = $2-1;} NUMBER                  
						{ 	if ($1 < 0) yyerror("Encountered a rule with negative literal as head.");
							addLit($4);
						}
			body ZERO  { CR(modsolver->addRule(modid, !disj, lits)); lits.clear(); }
		;
			
/*
 * bool 	addAggrExpr		(int modid, int head, int setid, Weight bound, bool lower, AggrType type, bool defined);
 * SUMDEFN  NUMBER SEMDEFN SIGNDEFN NUMBER NUMBER NUMBER ZERO
 * type		modid 	defined lower    head	setid	bound
 * addAggrExpr($2-1, $5, $6, Weight($7), $4, type, $3)
 */
			
magg	:	AGGDEFN  NUMBER SEMDEFN SIGNDEFN NUMBER NUMBER NUMBER ZERO	{ CR(modsolver->addAggrExpr($2-1, Literal($5), $6, Weight($7), $4, getAggrType($1), $3)); }
		;
magg	:	AGGDEFN  NUMBER SEMDEFN SIGNDEFN NUMBER NUMBER ZERO ZERO	{ CR(modsolver->addAggrExpr($2-1, Literal($5), $6, Weight(0), $4, getAggrType($1), $3)); }
		;

mset	:	SETDEFN NUMBER NUMBER { modid = $2-1; setid = $3; }
			body ZERO	{ 
						for(vector<Literal>::size_type i=0; i<lits.size(); i++){
								weights.push_back(1);
							}
							CR(modsolver->addSet(modid, setid, lits, weights));
							lits.clear(); weights.clear();
						}
		;

mwset	:	WSETDEFN NUMBER NUMBER { modid = $2-1; setid = $3; }
			wbody ZERO	{ 
						CR(modsolver->addSet(modid, setid, lits, weights));
						lits.clear(); weights.clear();
					}
		;
			
number	:	NUMBER	{ $$ = $1; }
		|	ZERO	{ $$ = 0; }

%%

int yywrap() {
	cerr << "End of file reached before it was expected... bailing out." << endl;
	return 1;
}
