/* Lexer for FlatZinc.
 * Nick Nethercote, started May 2007.
 *
 * This file is in the public domain, and can be used without copyright
 * restrictions.
 */

%{	
#include <string>
#include <vector>
#include "parser/flatzincsupport/FZDatastructs.hpp"
using namespace std;
using namespace FZ;

#include "parser/FZScanner.hpp"

#define FT FZ::FZParser::token

%}

%option noyywrap nounput never-interactive nodefault
%option prefix="fz"
%option c++ yyclass="FZScanner"
%option yylineno

    /* Regular expressions for attributed tokens. */
ident           	[A-Za-z][A-Za-z0-9_]*
underscore_ident	_*[A-Za-z][A-Za-z0-9_]*
string_literal  	\"[^"\n]*\"
int_literal     	-?[0-9]+|-?0x[0-9A-Fa-f]+|-?0o[0-7]+
float_literal   	-?[0-9]+\.[0-9]+|-?[0-9]+\.[0-9]+[Ee][-+]?[0-9]+|-?[0-9]+[Ee][-+]?[0-9]+

%%

    /* Reserved words */
"array"         { return FT::ARRAY;     }
"bool"          { return FT::BOOL;      }
"constraint"    { return FT::CONSTRAINT;}
"false"         { return FT::FALSE;     }
"float"         { return FT::FLOAT;     }
"int"           { return FT::INT;       }
"minimize"      { return FT::MINIMIZE;  }
"maximize"      { return FT::MAXIMIZE;  }
"of"            { return FT::OF;        }
"predicate"		{ return FT::PREDICATE; }
"satisfy"       { return FT::SATISFY;   }
"set"           { return FT::SET;       }
"solve"         { return FT::SOLVE;     }
"true"          { return FT::TRUE;      }
"var"           { return FT::VAR;       }

\.\.            { return FT::DOTDOT;    }
::              { return FT::COLONCOLON;}

    /* Attributed tokens */
{ident} { 
        fzlval->string_val = new string(yytext);
        return FT::IDENT; 
}
{underscore_ident} {
	fzlval->string_val = new string(yytext);
	return FT::UNDERSCORE_IDENT;
}
{string_literal} { 
        fzlval->string_val = new string(yytext);  
        return FT::STRING_LITERAL; 
}
{int_literal} {
        /*
	** atoi() doesn't recognise our hex and octal numbers, so we must
	** handle them ourselves.
        **
        ** XXX: this code doesn't detect if the integer literals overflow an
        ** 'int'.  (In particular, 'atoi' doesn't detect overflow errors,
        ** but 'strtol' does, so that could be used for the decimal integer
        ** case.)
        ** 
        ** XXX: I think this code will incorrectly parse negative octal and
        ** hexadecimal numbers -- the two conditions will fail due to the
        ** leading '-', so 'atoi' will be invoked, which will fail, and
        ** fzlval.int_val will be set to something bogus (probably zero).
	*/
        if        ('0' == yytext[0] && 'x' == yytext[1])  {
            int i = 2, x = 0;
            while ('\0' != yytext[i]) {
                x *= 16;
                if ('0' <= yytext[i] && yytext[i] <= '9') {
                    x += (yytext[i] - '0');
                } else {
                    x += (yytext[i] - 'a' + 10);
                }
                i++;
            }
            fzlval->int_val = x;

        } else if ('0' == yytext[0] && 'o' == yytext[1])  {
            int i = 2, x = 0;
            while ('\0' != yytext[i]) {
                x *= 8;
                x += (yytext[i] - '0');
                i++;
            }
            fzlval->int_val = x;

        } else {
            fzlval->int_val = atoi(yytext);
        }
        return FT::INT_LITERAL; 
    }
{float_literal} {
        fzlval->float_val = atof(yytext);
        return FT::FLOAT_LITERAL; 
    }

    /* Skip newlines, whitespace and comments, return any non-matched
     * character. */
\n          ;
[ \t]       ;
%.*         ;
.           { return yytext[0]; }

%%
