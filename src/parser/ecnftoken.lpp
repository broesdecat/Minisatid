%{
/*
 * Copyright 2007-2011 Katholieke Universiteit Leuven
 *
 * Use of this software is governed by the GNU LGPLv3.0 license
 *
 * Written by Broes De Cat and Maarten MariÃ«n, K.U.Leuven, Departement
 * Computerwetenschappen, Celestijnenlaan 200A, B-3001 Leuven, Belgium
 */
/* C++ declarations: */
#include <iostream>

/*
 * Temporarily, until new bison is available
 * SHOULD BE REMOVED THEN
 */
#include <vector>
#include "external/ExternalUtils.hpp"
using namespace std; 
//UP TILL HERE!

#include "parser.hh"

extern int lineNo;
extern int charPos;

#define ADJ (charPos += yyleng)
%}

/* lex definitions: */
%option noyywrap never-interactive nounput

%s theory
%s trans

ID			_*[A-Za-z]['a-zA-Z0-9_]*
INT			-?[0-9]+
WHITESPACE	[\t\r ]*

%%

<*>^"c grounder error".*	{ throw MinisatID::idpexception("There was a grounding error, so no solving is possible.\n");}


<*>^"p "			{ADJ; BEGIN(theory);}
<*>^"translation"	{ADJ; BEGIN(trans); return TRANSHEADER;}
<*>^"=== atom translation ==="	{ADJ; BEGIN(trans); return TRANSHEADER2;}

<theory,INITIAL>^"c\n"	{/* disregard comments */}
<theory,INITIAL>^"c ".*	{/* disregard comments */}

<theory>"cnf"	{ADJ; return CNF;}
<theory>"ecnf"	{ADJ; return ECNF;}
<theory>"def" 	{ADJ; return DEFPRESENT;}
<theory>"aggr" 	{ADJ; return AGGPRESENT;}
<theory>"mnmz" 	{ADJ; return MINIMPRESENT;}

<theory>^"WSet"	{ADJ; return WSETDEFN;}
<theory>^"Set"	{ADJ; return SETDEFN;}
<theory>"Card"	{ADJ; /*ecnflval*/yylval.aggr = MinisatID::AggType::CARD; return AGGDEFN;}
<theory>"Sum"	{ADJ; /*ecnflval*/yylval.aggr = MinisatID::AggType::SUM; return AGGDEFN;}
<theory>"Prod"	{ADJ; /*ecnflval*/yylval.aggr = MinisatID::AggType::PROD; return AGGDEFN;}
<theory>"Min"	{ADJ; /*ecnflval*/yylval.aggr = MinisatID::AggType::MIN; return AGGDEFN;}
<theory>"Max"	{ADJ; /*ecnflval*/yylval.aggr = MinisatID::AggType::MAX; return AGGDEFN;}

<theory>"<-"	{ADJ; return DEFOP;}

<theory>"D"		{ADJ; /*ecnflval*/yylval.sem = MinisatID::AggSem::DEF;  return SEMDEFN;}
<theory>"C"		{ADJ; /*ecnflval*/yylval.sem = MinisatID::AggSem::COMP;  return SEMDEFN;}
<theory>"O"		{ADJ; /*ecnflval*/yylval.sem = MinisatID::AggSem::OR;  return SEMDEFN;}

<theory>"L"		{ADJ; /*ecnflval*/yylval.sign = MinisatID::AggSign::UB;  return SIGNDEFN;}
<theory>"G"		{ADJ; /*ecnflval*/yylval.sign = MinisatID::AggSign::LB; return SIGNDEFN;}

<theory>^"Mnmsubset"	{ADJ; return SUBSETMINDEFN;}
<theory>^"Mnmlist"		{ADJ; return MNMZDEFN;}
<theory>^"Mnmvar"		{ADJ; return VARMINDEFN;}
<theory>^"Mnmagg"		{ADJ; return AGGMINDEFN;}

<theory>" "		{ADJ; /* disregard whitespaces */}
<theory>"\t"	{ADJ; /*                       */}
<theory>"\n"	{charPos = 1; lineNo++; /*     */}

<theory>"|"		{ADJ; return DELIM;}
<theory>"["		{ADJ; return SYMMSTART;}
<theory>"]"		{ADJ; return SYMMEND;}
<theory>"("		{ADJ; return CYCLESTART;}
<theory>")"		{ADJ; return CYCLEEND;}
<theory>","		{ADJ; return SYMMDELIM;}

<theory>^"INTVAR"	{ADJ; return INTVAR;}
<theory>^"INTVARDOM" {ADJ; return INTVARDOM;}
<theory>^"BINTRI"	{ADJ; return BINTRI;}
<theory>^"BINTRT"	{ADJ; return BINTRT;}
<theory>^"DISTINCT"	{ADJ; return DISTINCT;}
<theory>^"CPSUM"	{ADJ; return CPSUM;}
<theory>^"COUNT"	{ADJ; return COUNT;}
<theory>^"ELEMENT"	{ADJ; return ELEMENT;}

<theory>"="		{ADJ; /*ecnflval*/yylval.rel = MinisatID::EqType::EQ; return REL;}
<theory>">"		{ADJ; /*ecnflval*/yylval.rel = MinisatID::EqType::G; return REL;}
<theory>"<"		{ADJ; /*ecnflval*/yylval.rel = MinisatID::EqType::L; return REL;}
<theory>"~="	{ADJ; /*ecnflval*/yylval.rel = MinisatID::EqType::NEQ; return REL;}
<theory>"=<"	{ADJ; /*ecnflval*/yylval.rel = MinisatID::EqType::LEQ; return REL;}
<theory>">="	{ADJ; /*ecnflval*/yylval.rel = MinisatID::EqType::GEQ; return REL;}

 /* Longest match is returned -> if e.g. "20", NUMBER is returned.
    If "0", then ZERO is returned since it's the first matching rule.
  */
<theory>"0"			{ADJ; /*ecnflval*/yylval.integer = 0; return ZERO;}
<theory>-?[0-9]+	{ADJ; yylval.str = new string(yytext); return NUMBER;}
			
<trans>^"type"			{ return TYPE;				}
<trans>^"show pred"		{ return SHOWPRED;			}
<trans>^"pred"			{ return PRED;				}
<trans>^"show func"		{ return SHOWFUNC;			}
<trans>^"func"			{ return FUNC;				}
<trans>^"true"			{ return TRUE;				}
<trans>^"arbitrary"		{ return ARBIT;				}
<trans>{ID}				{ yylval.str = new string(yytext);	return T_ID;	}
<trans>{INT}			{ yylval.integer = atoi(yytext); 	return T_NUM;		}
<trans>{WHITESPACE}   	{                      		}
<trans>.				{ return *yytext;			}

<theory,INITIAL,trans>\n {} //IMPORTANT: lex . pattern does NOT match newlines, so match them explicitly!
<theory,INITIAL>.	{/* Anything else: parse error */
						char s[100];
						sprintf(s, "Encountered unexpected character '%s'.\n", yytext);
						throw MinisatID::idpexception(s);
					}
